;分页设置 线性地址 等于 物理地址
;开启分页需要在开启保护模式之后设置
;            Page Directory Entry (4KB Page Table)
; 31------------------------------------------12-11--9-8-7-6-5-4-3-2-1-0-
; |												|     | |P| | |P|P|U|R| |
; | Page Frame Address  bits 31-12              |AVL  |G|S|0|A|C|W|/|/|P|
; |                                             |     | | | | |D|T|S|W| |
; -----------------------------------------------------------------------

;            Page Table Entry (4KB Page)
; 31------------------------------------------12-11--9-8-7-6-5-4-3-2-1-0-
; |												|     | |P| | |P|P|U|R| |
; | Page Frame Address  bits 31-12              |AVL  |G|A|D|A|C|W|/|/|P|
; |                                             |     | |T| | |D|T|S|W| |
; -----------------------------------------------------------------------
; P  ——位0 是存在标志，用于指明表项对地址转换是否有效 P=1表示有效；P=0表示无效
; R/W——位1 是读写标志，1，表示页面可读可写可执行。0，表示页面只读或可执行，当处理器运行在超级用户特权级别（0,1,2）则R/W位不起作用
; U/S——位2 是用户/超级用户的标志。如果是1，那么运行在任何特权级上的程序都可以访问改页面。如果是0，那么页面只能被运行在超级用户特权级（0,1,2）上的程序访问
; PWT——位3 页级直写
; PCD——位4 缓存禁用标志
; A  ——位5 访问标志，当处理器访问页表项映射的页面时，也表项的这个标志就会被置为1，处理器只负责设置该标志，操作系统可通过定期地复位该标志来统计页面的使用情况
; D	 ——位6 页面以修改标志。当处理器对一个页面执行写操作时，就会设置对应页表表项的D标志。处理器并不会修改页目录项中的D标志
; PS ——位7 确定页的尺寸。该标志仅被用于页目录项。当该标志被清零时，页尺寸为4KB，页目录项指向一个页表。当该标志被置1时，页的尺寸为32位寻址的4MB。
; PAT——位7 页属性表索引标志
; G  ——位8 全局标志。当一个页被标明为全局，并且CR4中的启用全局页（PGE）标志被置位时，一旦CR3寄存器被载入或者发生任务切换，TLB中的页表或者指向页的目录项并不失效。
; AVL——位9,10,11 保留 可为软件所用

[bits 32]
global pg_dir,after_setup_page
[extern main]

pg_dir equ 0x0000				;页目录地址
pg0 equ 0x1000					;第一页表地址
pg1 equ 0x2000					;第二页表地址
pg2 equ 0x3000					;第三页表地址
pg3 equ 0x4000					;第四页表地址


after_setup_page:
	push main					;分页设置后的返回地址
	jmp setup_page				;分页

setup_page:
	mov ecx,1024*5				;对页目录地址以及页表地址5页每页4KB 一页1024项 全清零
	xor eax,eax
	xor edi,edi
	cld
	rep
	stosd						;从0x000地址开始将eax中的值复制到es[edi]中且edi增4  rep stosd dword ptr es:[edi], eax

	
	mov dword [pg_dir],pg0+7	;设置页目录项第一项 第一个页表地址0x1007   0x7=0111 页存在，用户可读可写
	mov dword [pg_dir+4],pg1+7
	mov dword [pg_dir+8],pg2+7
	mov dword [pg_dir+12],pg3+7
	
	mov dword edi,pg3+4092		;填写页表所有项 4个页面 每个页面1024项 一页4kb即4*1024*4kb =16MB 从最后一个页表最后一项开始按倒退顺序填写
	mov eax,0xfff007			;最后一个物理页面地址 0xfff000=16MB-4096   0x7=0111 页存在，用户可读可写
	
	std							;方向位置位
a:
	stosd						;将eax值赋值到es[edi]中且edi-4
	sub eax,0x1000				;eax-4096->eax中
	jge a						;如果大于等于0 则继续填充
	
	xor eax,eax					;eax置0
	mov cr3,eax					;现在设置页目录表基址寄存器cr3,cr3中保存的是页目录表的物理地址，设置页目录地址0x0000
	mov eax,cr0
	or  eax,0x80000000			;启动分页处理cr0的标志，位31
	mov cr0,eax

	ret
	





